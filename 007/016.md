Active Cotton Walrus

medium

# Hardcoded pool(fee) on contract **InfiltrationPeriphery**
## Summary

The `heal` and `costToHeal` functions of the contract **InfiltrationPeriphery** interact to V3SwapRouter, both function use the same liquidity pool because the `fee` of the pool it's constant

## Vulnerability Detail

The pool fee is constant 3000(0.3%), which means that the pool tier is always the same: `uint24 private constant POOL_FEE = 3_000;`

The `heal` function swap tokens from WETH to LOOKS using always the same pool:

```solidity
    function heal(uint256[] calldata agentIds) external payable {
        uint256 costToHealInLOOKS = INFILTRATION.costToHeal(agentIds);

        IV3SwapRouter.ExactOutputSingleParams memory params = IV3SwapRouter.ExactOutputSingleParams({
            tokenIn: WETH,
            tokenOut: LOOKS,
            fee: POOL_FEE,
            recipient: address(this),
            amountOut: costToHealInLOOKS,
            amountInMaximum: msg.value,
            sqrtPriceLimitX96: 0
        });

        uint256 amountIn = SWAP_ROUTER.exactOutputSingle{value: msg.value}(params);

        IERC20(LOOKS).approve(address(TRANSFER_MANAGER), costToHealInLOOKS);

        INFILTRATION.heal(agentIds);

        if (msg.value > amountIn) {
            SWAP_ROUTER.refundETH();
            unchecked {
                _transferETHAndWrapIfFailWithGasLimit(WETH, msg.sender, msg.value - amountIn, gasleft());
            }
        }
    }
```

The `costToHeal` function also has this problem although its severity is less since it is a `view` function:

```solidity
    function costToHeal(uint256[] calldata agentIds) external returns (uint256 costToHealInETH) {
        uint256 costToHealInLOOKS = INFILTRATION.costToHeal(agentIds);

        IQuoterV2.QuoteExactOutputSingleParams memory params = IQuoterV2.QuoteExactOutputSingleParams({
            tokenIn: WETH,
            tokenOut: LOOKS,
            amount: costToHealInLOOKS,
            fee: POOL_FEE,
            sqrtPriceLimitX96: uint160(0)
        });

        (costToHealInETH, , , ) = QUOTER.quoteExactOutputSingle(params);
    }
```

The pools in [UniswaV3 are differentiated between the fee](https://docs.uniswap.org/concepts/protocol/fees#pool-fees-tiers), being able to generate several pools for the same pair, when hardcoded the `fee` the user is forced to use that single pool

It may be the case that the liquidity of the pool of 3000(0.3%) is changed to the other pool with lower fee, for example 500(0.05%) or 100(0.01%)

## Impact

The contract is limited to always using the same pool

## Code Snippet

https://github.com/sherlock-audit/2023-10-looksrare/blob/main/contracts-infiltration/contracts/InfiltrationPeriphery.sol#L19

https://github.com/sherlock-audit/2023-10-looksrare/blob/main/contracts-infiltration/contracts/InfiltrationPeriphery.sol#L48-L58

## Tool used

Manual Review

## Recommendation

Add `fee` parameter to the `heal` and `costToHeal` functions, with this the user can select the most convenient pool:

```diff
@@ -16,8 +16,6 @@ contract InfiltrationPeriphery is LowLevelWETH {
     address public immutable WETH;
     address public immutable LOOKS;
 
-    uint24 private constant POOL_FEE = 3_000;
-
     constructor(
         address _transferManager,
         address _infiltration,
@@ -42,13 +40,13 @@ contract InfiltrationPeriphery is LowLevelWETH {
      * @notice Submits a heal request for the specified agent IDs.
      * @param agentIds The agent IDs to heal.
      */
-    function heal(uint256[] calldata agentIds) external payable {
+    function heal(uint256[] calldata agentIds, uint24 fee) external payable {
         uint256 costToHealInLOOKS = INFILTRATION.costToHeal(agentIds);
 
         IV3SwapRouter.ExactOutputSingleParams memory params = IV3SwapRouter.ExactOutputSingleParams({
             tokenIn: WETH,
             tokenOut: LOOKS,
-            fee: POOL_FEE,
+            fee: fee,
             recipient: address(this),
             amountOut: costToHealInLOOKS,
             amountInMaximum: msg.value,
@@ -75,14 +73,14 @@ contract InfiltrationPeriphery is LowLevelWETH {
      * @param agentIds The agent IDs to heal.
      * @return costToHealInETH The cost to heal the specified agents.
      */
-    function costToHeal(uint256[] calldata agentIds) external returns (uint256 costToHealInETH) {
+    function costToHeal(uint256[] calldata agentIds, uint24 fee) external returns (uint256 costToHealInETH) {
         uint256 costToHealInLOOKS = INFILTRATION.costToHeal(agentIds);
 
         IQuoterV2.QuoteExactOutputSingleParams memory params = IQuoterV2.QuoteExactOutputSingleParams({
             tokenIn: WETH,
             tokenOut: LOOKS,
             amount: costToHealInLOOKS,
-            fee: POOL_FEE,
+            fee: fee,
             sqrtPriceLimitX96: uint160(0)
         });
```