Quiet Sandstone Osprey

medium

# Calls to `InfiltrationPeriphery.sol#heal` may be frontrun to achieve poor price execution for an unsuspecting caller.
## Summary

The [`heal`](https://github.com/sherlock-audit/2023-10-looksrare/blob/86e8a3a6d7880af0dc2ca03bf3eb31bc0a10a552/contracts-infiltration/contracts/InfiltrationPeriphery.sol#L45) and [`costToHeal`](https://github.com/sherlock-audit/2023-10-looksrare/blob/86e8a3a6d7880af0dc2ca03bf3eb31bc0a10a552/contracts-infiltration/contracts/InfiltrationPeriphery.sol#L78) functions utilize `ExactOutputSingleParams` which specify inefficient trade execution at the expense of the caller, explicitly indicating that they wish to execute their swap at any tick price regardless of market conditions.

## Vulnerability Detail

When allocating `QuoteExactOutputSingleParams`, both  [`heal`](https://github.com/sherlock-audit/2023-10-looksrare/blob/86e8a3a6d7880af0dc2ca03bf3eb31bc0a10a552/contracts-infiltration/contracts/InfiltrationPeriphery.sol#L45) and [`costToHeal`](https://github.com/sherlock-audit/2023-10-looksrare/blob/86e8a3a6d7880af0dc2ca03bf3eb31bc0a10a552/contracts-infiltration/contracts/InfiltrationPeriphery.sol#L78) functions specify a `sqrtPriceLimitX96` of `0`. From the [Uniswap V3 Single Swaps Documentation](https://docs.uniswap.org/contracts/v3/guides/swaps/single-swaps):

> `sqrtPriceLimitX96`: We set this to zero - which makes this parameter inactive. In **production**, this value can be used to set the limit for the price the swap will push the pool to, which can **help protect against price impact** or for setting up logic in a variety of price-relevant mechanisms.

This means that the trades made using these parameters are **not protected against price impact**.

Moreover, the codebase would suggest to a casual observer that paying over-the-odds for their trade would be safe, given the comprehensive refund mechanism:

```solidity
 if (msg.value > amountIn) {
    SWAP_ROUTER.refundETH();
    unchecked {
      _transferETHAndWrapIfFailWithGasLimit(WETH, msg.sender, msg.value - amountIn, gasleft());
    }
}
```

Disguised here, however, is the implicit agreement to execution of the order at the current market price, regardless of the state of the pool - which can be drastically manipulated by frontrunning bots that anticipate the transaction.

It is important to note that the `public view` function `costToHeal` will not be correctly indicative of this adverse execution-time market behaviour.

## Impact

**For any amount of `msg.value` sent by the caller that is greater than or equal to the current market price for $LOOKS, callers will receive the exact amount of tokens requested.**

When considering transactions made in haste (which is a reasonable expectation given the intended size of the audience versus the relative scarcity of healing), this could lead to significant unintentional material losses for unsuspecting users.

Meanwhile, orders executed at the `msg.value` predicted exactly by `costToHeal` may not be executed in a timely manner compared to higher-value competing swaps, reducing the likelihood of success for players intending to heal their agents.

## Code Snippet

```solidity
/**
 * @notice Submits a heal request for the specified agent IDs.
 * @param agentIds The agent IDs to heal.
 */
function heal(uint256[] calldata agentIds) external payable {
    uint256 costToHealInLOOKS = INFILTRATION.costToHeal(agentIds);

    IV3SwapRouter.ExactOutputSingleParams memory params = IV3SwapRouter.ExactOutputSingleParams({
        tokenIn: WETH,
        tokenOut: LOOKS,
        fee: POOL_FEE,
        recipient: address(this),
        amountOut: costToHealInLOOKS,
        amountInMaximum: msg.value,
        sqrtPriceLimitX96: 0 /* @audit poor execution price */
    });

    uint256 amountIn = SWAP_ROUTER.exactOutputSingle{value: msg.value}(params);

    IERC20(LOOKS).approve(address(TRANSFER_MANAGER), costToHealInLOOKS);

    INFILTRATION.heal(agentIds);

    if (msg.value > amountIn) {
        SWAP_ROUTER.refundETH();
        unchecked {
            _transferETHAndWrapIfFailWithGasLimit(WETH, msg.sender, msg.value - amountIn, gasleft());
        }
    }
}
```

## Tool used

Manual Review, Visual Studio Code and [Uniswap V3 Single Swaps Documentation](https://docs.uniswap.org/contracts/v3/guides/swaps/single-swaps)

## Recommendation

Prior to submitting a `heal` transaction, the application frontend or user can decide upon a fair trade execution price, and the subsequent call to `InfiltrationPeripiphery` must accept the user's selection of `sqrtPriceLimitX96`:

```diff
- function heal(uint256[] calldata agentIds) external payable {
+ function heal(uint256[] calldata agentIds, uint160 sqrtPriceLimitX96) external payable {
```

```diff
- function costToHeal(uint256[] calldata agentIds) external returns (uint256 costToHealInETH) {
+ function costToHeal(uint256[] calldata agentIds, uint160 sqrtPriceLimitX96) external returns (uint256 costToHealInETH) {
```