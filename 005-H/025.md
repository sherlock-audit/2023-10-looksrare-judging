Fun Aegean Halibut

high

# An agent wounded-healed-wounded during ROUNDS_TO_BE_WOUNDED_BEFORE_DEAD rounds can be unjustly killed
## Summary
When an agent is wounded, they have `ROUNDS_TO_BE_WOUNDED_BEFORE_DEAD` rounds to initiate healing, otherwise the contract will mark them as `Dead` during new round.

However the contract fails to take in account that an agent can heal and be wounded again before `ROUNDS_TO_BE_WOUNDED_BEFORE_DEAD` has elapsed, and in that case would kill the agent, as if they were never healed.

## Vulnerability Detail
We see that the smart contract keeps track of agents which are wounded at some round `R`, with the variable:
`woundedAgentIdsPerRound[R]`, which is an array of ids.

This way when initializing a new round in `fulfillRandomness`, the call iterates over the agent ids located at index:
```solidity
roundWoundedToKill = currentRound - ROUNDS_TO_BE_WOUNDED_BEFORE_DEAD
```
And marks as `Dead` any agent which is still in the state `Wounded`.
However if the agent has healed, and been wounded again between `roundWoundedToKill` and `currentRound`, he would be unjustly marked as `Dead`, when he has only been wounded recently.

See the implementation of `_killWoundedAgents`:
https://github.com/sherlock-audit/2023-10-looksrare/blob/main/contracts-infiltration/contracts/Infiltration.sol#L1477-L1508

## Impact
Some agents may be unjustly marked as dead during the game, participants unjustly lose their shot at winning a prize (loss of funds for the participants). 

## Code Snippet

## Tool used

Manual Review

## Recommendation
Modify `_killWoundedAgents` to check the `woundedAt` variable:
```diff
function _killWoundedAgents(
    uint256 roundId,
    uint256 currentRoundAgentsAlive
) private returns (uint256 deadAgentsCount) {
    uint16[MAXIMUM_HEALING_OR_WOUNDED_AGENTS_PER_ROUND_AND_LENGTH]
        storage woundedAgentIdsInRound = woundedAgentIdsPerRound[roundId];
    uint256 woundedAgentIdsCount = woundedAgentIdsInRound[0];
    uint256[] memory woundedAgentIds = new uint256[](woundedAgentIdsCount);
    for (uint256 i; i < woundedAgentIdsCount; ) {
        uint256 woundedAgentId = woundedAgentIdsInRound[i.unsafeAdd(1)];

        uint256 index = agentIndex(woundedAgentId);
        
        if (agents[index].status == AgentStatus.Wounded && 
+            //Ensure agents were last wounded at that round, and not healed/rewounded
+            agents[index].woundedAt == roundId
        ) {
            woundedAgentIds[i] = woundedAgentId;
            _swap({
                currentAgentIndex: index,
                lastAgentIndex: currentRoundAgentsAlive - deadAgentsCount,
                agentId: woundedAgentId,
                newStatus: AgentStatus.Dead
            });
            unchecked {
                ++deadAgentsCount;
            }
        }

        unchecked {
            ++i;
        }
    }

    emit Killed(roundId, woundedAgentIds);
}
```